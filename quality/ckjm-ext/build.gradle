plugins {
  id 'java'
  id 'application'
  id 'jacoco'
  // Licensing Plugin
  id "com.github.hierynomus.license-base"// version "0.16.1"

  // Lombok Gradle Plugin
  id "io.freefair.lombok"// version "6.5.0-rc1"

  id 'maven-publish'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.apache.bcel:bcel:6.5.0'
  implementation 'org.apache.ant:ant:1.10.11'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
  testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
  testImplementation 'org.testng:testng:7.6.0'
  testImplementation 'org.assertj:assertj-core:3.21.0'

  implementation 'org.apache.commons:commons-math3:3.6.1'
  implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
  implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
  implementation 'commons-cli:commons-cli:1.5.0'

  implementation 'com.google.code.gson:gson:2.9.0'
  implementation 'com.google.guava:guava:30.1.1-jre'

  implementation 'org.yaml:snakeyaml:1.25'

  runtimeOnly 'com.github.peichhorn:lombok-pg:0.11.3'

  // Annotations
  implementation 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.1.0'
  implementation 'org.jetbrains:annotations:15.0'
}

javadoc {
   source = sourceSets.main.allJava
   classpath = configurations.implementation
   destinationDir = file("${rootDir}/docs/javadoc")
}

tasks.named('test') {
   // Use junit platform for unit tests.
   useJUnitPlatform()
}

jacoco {
   toolVersion = "0.8.6"
   reportsDirectory = file("$buildDir/jacoco")
}

test {
  finalizedBy jacocoTestReport
}

jacocoTestReport {
   dependsOn test
   reports {
       xml.enabled false
       csv.enabled false
       html.destination file("${buildDir}/jacocoHtml")
   }
}

jacocoTestCoverageVerification {
   violationRules {
       rule {
           limit {
               counter = 'LINE'
               value = 'COVEREDRATIO'
               minimum = 0.85
           }
       }
   }
}

license {
   header = project.file('LICENSE')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'dev.siliconcode.ckjm'
      artifactId = 'ckjm_ext'
      version = '2.5'
      pom {
        name = 'ckjm_ext'
        description = 'CKJM extended - An extended version of Tool for Calculating Chidamber and Kemerer Java Metrics (and many other metrics)'
        url = 'http://www.empirilytics.com/ckjm_ext'
        license {
          license {
            name = 'Creative Commons Attribution-Noncommercial-No Derivative Works 2.5 License'
            url = 'http://creativecommons.org/licenses/by-nc-nd/2.5/'
          }
        }
        developers {
          developer {
            name = "Diomidis Spinellis"
          }
          developer {
            name = "Julian Rentrop"
          }
          developer {
            name = 'Marian Jureczko'
            email = 'marian.jureczko@gmail.com'
          }
          developer {
            name = 'Isaac Griffith'
            email = 'isaacgriffith@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:empirilytics/CKJM-extended.git'
          developerConnection = 'scm:git:git@github.com:empirilytics/CKJM-extended.git'
          tag = 'HEAD'
        }
      }
    }
  }
}

application {
  mainClass = 'dev.siliconcode.ckjm.app.Main'
  applicationName = 'ckjm-ext'
}
